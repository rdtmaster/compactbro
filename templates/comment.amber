mixin comment($c)
	$likes = likesInt($c.Likes)
	div
		.thing
		.comment
		.collapsed?$c.Collapsed
		[data-id=$c.FullID]
		+midcol1($likes)
		div
			.entry
			.dislikes?$likes == -1
			.unvoted?$likes == 0
			.likes?$likes == 1
				div.tagline
					+author($c.Author, getDistinguished($c.Distinguished,$c.IsSubmitter))
					+authorFlair($c.AuthorFlairRichtext, $c.AuthorFlairText, $c.AuthorFlairBackground, $c.AuthorFlairColor)
					+score($c.Score)
					span.live-timestamp[title=$c.Created]#{dateAgo($c.Created)}
					if $c.TotalAwards > 0
						+awardings($c.Awardings)
					if $c.Stickied
						span[style="float:right"].stickied-tagline[title="selected by this subreddit's moderators"] stickied comment
			a.options_link[href="#"][onclick="return optionsDisplay(this)"]
			form.usertext[action="#"][method="POST"][onsubmit="return editSubmit(this)"]
				input[name="thing_id"][value=$c.FullID][type="hidden"]
				div.usertext-body#{html($c.Body_html)}
				if isMine($c.Author)
					div.usertext-edit.hidden
						textarea[name="selftext"]#{unescaped($c.Body)}
						div.bottom-area
							strong
								span.error.placeholder.field-body
							div.usertext-buttons
								button.save.newbutton[type="submit"] Save
								button.newbutton[type="submit"] Delete comment
						div[style="clear:both"]
			div.clear.options_expando.hidden
				a[href="#"][onclick="return unimpl()"]
					div.email-icon
					|Share
				if isPostID($c.ParentID)
					a[href=cleanLink($c.PostPermalink)]
						div.parent-icon
						|Parent
				else
					a[href=cleanLink($c.PostPermalink)+cleanCommentID($c.ParentID)+"/"]
						div.parent-icon
						|Parent
				a[href="#"][onclick="return replyFormDisplay(this)"]
					div.reply-icon
					|Reply
				a[href=$c.Context]
					div.context-icon
					|Context
				a[href=$c.Permalink]
					div.permalink-icon
					|Permalink
				a[href="#"][onclick="return collapseComment(this)"]
					div.collapse-icon
					|Collapse
				a[href="#"][onclick="return editFormDisplay(this)"]
					div.edit-icon
					|Edit
				
			
		
		form.hidden.usertext[action="#"][onsubmit="return submitComment(this);"]
			input[name="thing_id"][value=$c.FullID][type="hidden"]
			textarea.replytext[name="text"][cols=1][rows=1]
			div.bottom-area
				strong
					span.error.hidden
				div.usertext-buttons
					button.save.newbutton[type="submit"] Reply
				div[style="clear:both"]
		div.commentspacer
		if hasReplies($c)
			div.child
				div.sitetable.listing#{html(processReplies($c.Replies))}
		if $c.HasMore()
			div.child
				div.sitetable.listing
					span.deepthread.button
						a[href=$c.PostPermalink+$c.ID+"/"] Continue this thread