mixin comment($c)
	$likes = likesInt($c.Likes)
	div
		.thing
		.comment
		.collapsed?$c.Collapsed
		[data-id=$c.FullID]
		+midcol1($likes)
		div
			.entry
			.dislikes?$likes == -1
			.unvoted?$likes == 0
			.likes?$likes == 1
				div.tagline
					+author($c.Author, getDistinguished($c.Distinguished,$c.IsSubmitter))
					+authorFlair($c.AuthorFlairRichtext, $c.AuthorFlairText, $c.AuthorFlairBackground, $c.AuthorFlairColor)
					+score($c.Score)
					span.live-timestamp[title=$c.Created]#{dateAgo($c.Created)}
					if $c.TotalAwards > 0
						+awardings($c.Awardings)
					if $c.Stickied
						span[style="float:right"].stickied-tagline[title="selected by this subreddit's moderators"] stickied comment
			a.options_link[href="#"][onclick="return optionsDisplay(this)"]
			form.usertext[action="#"][method="POST"][onsubmit="return editSubmit(this)"]
				input[name="thing_id"][value=$c.FullID][type="hidden"]
				div.usertext-body#{html($c.Body_html)}
				if isMine($c.Author)
					div.usertext-edit.hidden
						textarea[name="selftext"]#{unescaped($c.Body)}
						div.bottom-area
							strong
								span.error.placeholder.field-body
							div.usertext-buttons
								button.save.newbutton[type="submit"] Save
								button.newbutton[type="submit"] Delete comment
						div[style="clear:both"]
			div.clear.options_expando.hidden
				a[href="#"][onclick="return unimpl()"]
					div.email-icon
					|Share
				a[href="#"][onclick="return replyFormDisplay(this)"]
					div.reply-icon
					|Reply
				if isMine($c.Author)
					a[href="#"][onclick="unimpl()"]
						div.context-icon
						|Context
				a[href="#"][onclick="unimpl()"]
					div.permalink-icon
					|Permalink
				a[href="#"][onclick="return collapseComment(this)"]
					div.collapse-icon
					|Collapse
				a[href="#"][onclick="return editFormDisplay(this)"]
					div.edit-icon
					|Edit
		div.child.commentreply.hidden
				div.thing.comment
					form.usertext[action="#"][onsubmit="return submitComment(this);"]
						input[name="thing_id"][value=$c.FullID][type="hidden"]
						textarea[name="text"][cols=1][rows=1]
						div.bottom-area
							strong
								span.error.hidden
							div.usertext-buttons
								button.save.newbutton[type="submit"] Reply
							div[style="clear:both"]
		div.commentspacer
		if hasReplies($c)
			div.child #{html(processReplies($c.Replies))}