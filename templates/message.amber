mixin message($m)
	$likes = likesInt($m.Likes)
	div
		.thing
		.message
		.was_comment?$m.IsComment
		.unread?$m.New
		if $m.IsComment
			+midcol2($likes)
		div.entry
			.unvoted ? $likes == 0
			.likes ? $likes == 1
			.dislikes ? $likes == -1
			p.subject
				span #{$m.Subject}
				if $m.IsComment
					a.title[href=linkFromContext($m.Context)]#{$m.LinkTitle}
					
			p.tagline
				|from&nbsp;
				span
					if !isMine($m.Author)
						+author($m.Author, getDistinguished($m.Distinguished,2==1))
					if !isMine($m.To)
						|&nbsp;to&nbsp;
						span.correspondent
							a.author.may-blank[href="/u/"+$m.To+"/"]#{$m.To}
				if $m.IsComment
					|&nbsp;via&nbsp;
					span.subreddit
						a[href=$m.SubredditNamePrefixed]#{$m.SubredditNamePrefixed}
				|&nbsp;sent&nbsp;
				time.live-timestamp[title=$m.Created]#{dateAgo($m.Created)}
			div.usertext-body#{html($m.BodyHtml)}
		a.options_link[href="#"][onclick="return optionsDisplay(this)"]
		div.clear.options_expando.hidden
			a[href="#"][onclick="return unimpl()"]
				div.email-icon
				|Share
			a[href="#"][onclick="return unimpl()"]
				div.parent-icon
				|Parent
			if $m.IsComment
				a[href="#"][onclick="return msgReplyFormDisplay(this)"]
					div.reply-icon
					|Reply
			else
				a[href="#"][onclick="return unimpl()"]
					div.reply-icon
					|Reply

		form.hidden.usertext[action="#"][onsubmit="return submitComment(this);"]
			input[name="thing_id"][value=$m.FullID][type="hidden"]
			textarea.replytext[name="text"][cols=1][rows=1]
			div.bottom-area
				strong
					span.error.hidden
				div.usertext-buttons
					button.save.newbutton[type="submit"] Reply
				div[style="clear:both"]