mixin postList($p, $ptt)
	$likes = likesInt($p.Likes)
	div
		[class = "thing link id-"+$p.FullID]
		.stickied ? $p.Stickied
		span.rank
		+midcol1($likes)
		if $ptt == "0"
			div.commentcount
				a.comments.may-blank[href=$p.Permalink] #{$p.NumberOfComments}
		div
			.entry
			.unvoted ? $likes == 0
			.likes ? $likes == 1
			.dislikes ? $likes == -1
			if strNotEmpty($p.LinkFlairText)
				span.linkflair #{$p.LinkFlairText}
			p.title
				a.may-blank[href=$p.Permalink] #{$p.Title}
				span.domain (self)
			if $ptt == "0"
				a.expando-button.collapsed.selftext[href="#"][data-id=$p.FullID]
			div.tagline
				span
					span.score.dislikes #{$p.Score - 1} points
					span.score.unvoted #{$p.Score} points
					span.score.likes #{$p.Score + 1} points
					|&#32;Submitted&#32;
					time.live-timestamp[title=$p.Created]#{dateAgo($p.Created)}
				span
					|&#32;by&#32;
					span.flairrichtext.flair
					a
						[class=$p.Distinguished] ? strNotEmpty($p.Distinguished)
						.author
						.may-blank
						[href="/u/"+$p.Author]
						|#{$p.Author}
				if $p.TotalAwards > 0
					+awardings($p.Awardings)
		a.options_link[href="#"][data-id=$p.FullID]
		div.expando
			.toggleable ? $ptt == "0"
			[style="display:block"] ? $ptt == "1"
			[style="display:none"] ? $ptt == "0"
			form.usertext[action=""][method="POST"][data-id=$p.FullID]
				input[type="hidden"][name="thing_id"][value=$p.FullID]
				div.usertext-body #{html($p.Selftext_html)}
				if isMine($p.Author)
					div.usertext-edit.md-container[style="display:none"]
						div.md
							textarea[name="selftext"] #{$p.Body}
							strong
								span.error.placeholder.field-body
							div.usertext-buttons
								button.save.newbutton[type="submit"] Save
		div.clear.options_expando[style="display:none"]
			a[href="#"][onclick="unimpl()"]
				div.email-icon
				|Share
			if isMine($p.Author)
				a[href="#"][data-id=$p.FullID].editButton
					div.edit-icon
					|Edit