mixin postList($p, $ptt)
	$likes = likesInt($p.Likes)
	$thumb = getThumb($p.Preview)
	div
		.thing
		.link
		.stickied ? $p.Stickied
		[data-id=$p.FullID]
		span.rank
		+midcol1($likes)
		if $ptt == "0"
			div.commentcount
				a.comments.may-blank[href=$p.Permalink] #{$p.NumberOfComments}
		div
			.entry
			.unvoted ? $likes == 0
			.likes ? $likes == 1
			.dislikes ? $likes == -1
			if strNotEmpty($thumb)
				a.thumbnail.may-blank[href=$p.URL]
					img[height=70][width=70][src=$thumb]
			if strNotEmpty($p.LinkFlairText)
				span.linkflair #{$p.LinkFlairText}
			p.title
				a.may-blank[href=$p.Permalink] #{$p.Title}
				if $p.IsSelfPost
					span.domain (self)
				else
					span.domain (#{$p.Domain})
			if $ptt == "0"
				a.expando-button.collapsed.selftext[href="#"][onclick="return postBodyDisplay(this)"]
			div.tagline
				span
					+score($p.Score)
					|&#32;Submitted&#32;
					time.live-timestamp[title=$p.Created]#{dateAgo($p.Created)}
				span
					|&#32;by&#32;
					span.flairrichtext.flair
					+author($p.Author, getDistinguished($p.Distinguished,(2==1)))
				if $p.TotalAwards > 0
					+awardings($p.Awardings)
		a.options_link[href="#"][onclick="return optionsDisplay(this)"]
		div.expando
			.hidden?$ptt == "0"
			form.usertext[action=""][method="POST"][onsubmit="return editSubmit(this)"]
				input[type="hidden"][name="thing_id"][value=$p.FullID]
				div.usertext-body#{html($p.Selftext_html)}
				if isMine($p.Author)
					div.md.usertext-edit.hidden
						textarea[name="selftext"]#{unescaped($p.Body)}
						strong
							span.error.placeholder.field-body
						div.usertext-buttons
							button.save.newbutton[type="submit"] Save
		div.clear.options_expando.hidden
			a[href="#"][onclick="unimpl()"]
				div.email-icon
				|Share
			if isMine($p.Author)
				a[href="#"][onclick="return editDisplay(this)"].editButton
					div.edit-icon
					|Edit