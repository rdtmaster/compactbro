mixin link($p, $ptt)
	$likes = likesInt($p.Likes)
	$thumb = getThumb($p.Preview)
	div
		.thing
		.link
		.stickied ? $p.Stickied
		[id=$p.FullID]
		span.rank
		+midcol1($likes)
		if $ptt == "0"
			div.commentcount
				a.comments.may-blank[href=$p.Permalink] #{$p.NumberOfComments}
		div
			.entry
			.unvoted ? $likes == 0
			.likes ? $likes == 1
			.dislikes ? $likes == -1
			if strNotEmpty($thumb)
				a.thumbnail.may-blank[href=$p.URL]
					img[height=70][width=70][src=$thumb]
			if strNotEmpty($p.LinkFlairText)
				span
					.linkflair
					.flaircolordark? $p.LinkFlairColor == "dark"
					.flaircolorlight? $p.LinkFlairColor == "light"
					.noshadow?strNotEmpty($p.LinkFlairBackground) && $p.LinkFlairBackground != "transparent"
					[style="background: "+$p.LinkFlairBackground]? strNotEmpty($p.LinkFlairBackground) && $p.LinkFlairBackground != "transparent"
					[title=unescaped($p.LinkFlairText)]
					|#{unescaped($p.LinkFlairText)}
			p.title
				if $p.IsSelfPost
					a.may-blank[title=unescaped($p.Title)][href=$p.Permalink] #{unescaped($p.Title)}
					span.domain (self)
				else
					a.may-blank[title=unescaped($p.Title)][href=$p.URL] #{unescaped($p.Title)}
					span.domain (#{unescaped($p.Domain)})
				if $p.NSFW
					acronym[title="Adult content: Not Safe For Work"].nsfw-warning NSFW
				if $p.Spoiler
					acronym[title="Spoiler"].spoiler-warning SPOILER
			if $ptt == "0"
				a.expando-button.collapsed.selftext[href="#"][onclick="return postBodyDisplay(this)"]
			div.tagline
				span
					+score($p.Score)
					|&#32;Submitted&#32;
					time.live-timestamp[title=$p.Created]#{dateAgo($p.Created)}
				span
					|&#32;by&#32;
					+author($p.Author, getDistinguished($p.Distinguished,(2==1)))
				if strNotEmpty($p.AuthorFlairText) || len($p.AuthorFlairRichtext)>0
					+authorFlair($p.AuthorFlairRichtext, $p.AuthorFlairText, $p.AuthorFlairBackground, $p.AuthorFlairColor)
				span.subLink
					|&nbsp;to&nbsp;
					a[href="/"+$p.SubredditNamePrefixed]#{$p.SubredditNamePrefixed}
				if $p.TotalAwards > 0
					+awardings($p.Awardings)
				if $p.Stickied && !$p.Pinned
					span.stickied-tagline[title="announcement"]&nbsp;-&nbsp;announcement
		a.options_link[href="#"]
		div.expando
			.hidden?$ptt == "0"
			form.usertext[action=""][method="POST"][onsubmit="return editSubmit(this)"]
				input[type="hidden"][name="thing_id"][value=$p.FullID]
				div.usertext-body#{html($p.Selftext_html)}
				if isMine($p.Author)
					div.md.usertext-edit.hidden
						textarea[name="selftext"]#{unescaped($p.Body)}
						strong
							span.error.placeholder.field-body
						div.usertext-buttons
							button.save.newbutton[type="submit"] Save
		div.clear.options_expando.hidden
			a[href="#"]
				div.email-icon
				|Share
			if isMine($p.Author)
				a[href="#"]
					div.edit-icon
					|Edit